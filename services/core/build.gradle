// ############################################################################
// Source Sets & BuildScript
// ############################################################################
sourceSets {
    generated {
        java {
            srcDir 'src/generated/java'
        }
    }	
}

buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'org.flywaydb:flyway-gradle-plugin:3.0'
	}
}

// ############################################################################
// Dependencies
// ############################################################################
dependencies {
	// Hibernate & JPA 2
	//compile 	group: 'org.hibernate',				name: 'hibernate-core', 		version: '4.3.5.Final'
	compile		group: 'javax',						name: 'javaee-api', 			version: '7.0'

	// Query DSL Dependency
	compile 	group: 'com.mysema.querydsl', 		name: 'querydsl-apt', 			version: '3.4.1'
	compile 	group: 'com.mysema.querydsl', 		name: 'querydsl-jpa', 			version: '3.4.1'
	compile 	group: 'com.mysema.querydsl', 		name: 'querydsl-codegen', 		version: '3.4.1'
}

// ############################################################################
// Scaffolding of project structure
// ############################################################################
apply from: 'scaffold.gradle'

// ############################################################################
// Query DSL Generation
// ############################################################################
configurations {
	querydslapt
}

task generateQueryDSL(type: JavaCompile, group: 'build', description: 'Generate the Query DSL types') {
	source = sourceSets.main.java
	classpath = configurations.compile + configurations.querydslapt
	options.compilerArgs = [
		"-proc:only",
		"-processor", "com.mysema.query.apt.jpa.JPAAnnotationProcessor"
	]
	destinationDir = sourceSets.generated.java.srcDirs.iterator().next()
}

compileJava {
	dependsOn generateQueryDSL
	source generateQueryDSL.destinationDir
}

// ############################################################################
// Database
// ############################################################################
apply plugin: 'flyway'

flyway {
	driver = "org.hsqldb.jdbcDriver"
	url = "jdbc:hsqldb:file:/tmp/db;shutdown=true"
	user = "sa"
	password = ""
}

// ############################################################################
// Eclipse Project Nature for generated sources
// ############################################################################
eclipse {
	project {
		natures 'org.eclipse.xtext.ui.shared.xtextNature'
		buildCommand 'org.eclipse.xtext.ui.shared.xtextBuilder'
	}
}